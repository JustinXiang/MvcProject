apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.flx.administrator.mvcproject"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }

    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
        res.srcDirs =
                [
                        'src/main/res/layouts/act',
                        'src/main/res/layouts/fragment',
                        'src/main/res/layouts/comm',
                        'src/main/res/layouts/item',
                        'src/main/res/layouts/dialog',
                        'src/main/res/layouts/other',
                        'src/main/res/layouts',
                        'src/main/res'
                ]
    }

    signingConfigs {
        release {
            try {
                storeFile file("D:/Android/key/fengye.keystore")//这里替换成你自己项目生成的keystore的存储路径
                storePassword "123456"
                keyAlias "fengye"
                keyPassword "123456"
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }

    }

    buildTypes {
        release {
            //关闭log打印
            buildConfigField("boolean", "LOG_DEBUG", "false")
            buildConfigField "String", "SERVER_HOST", "\"正式ip\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //是否清理无用资源
            shrinkResources true
            //是否启用zipAlign压缩
            zipAlignEnabled true
            pseudoLocalesEnabled true
            signingConfig signingConfigs.release
        }

        debug {
            buildConfigField("boolean", "LOG_DEBUG", "true")
            buildConfigField "String", "SERVER_HOST", "\"http://m-dev.fengyelicai.com/\""
            minifyEnabled false//true：启用混淆,false:不启用
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            zipAlignEnabled true
            pseudoLocalesEnabled true
            signingConfig signingConfigs.debug
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "项目名字_${variant.productFlavors[0].name}__v${versionName}.apk"
        }
    }

    flavorDimensions "default"
    productFlavors {
        //配置多渠道打包
        huawei {
            dimension "default"
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "huawei", APP_NAME: "项目名字"]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.alibaba:fastjson:1.2.7'
    //图片
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    //网络请求相关
    implementation 'io.reactivex.rxjava2:rxjava:2.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
}
